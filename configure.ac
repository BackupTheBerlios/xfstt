# Process this file with autoconf to produce a configure script.
#
# xfstt configure.ac
#

AC_INIT([xfstt], [1.7])
AC_PREREQ([2.53])
AC_CONFIG_SRCDIR([src/xfstt.cc])
AC_CONFIG_AUX_DIR([config])
AC_CONFIG_HEADERS([config.h])

AM_INIT_AUTOMAKE([foreign 1.5])

m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])],
                            [AC_SUBST([AM_DEFAULT_VERBOSITY], [1])])

AC_ARG_WITH([x11],
            [AS_HELP_STRING([--with-x11], [enable tools using X11])],
            [],
            [with_x11=no])
AM_CONDITIONAL(WITH_X11, [test "x$with_x11" != "xno"])

# Checks for programs.
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PROG_RANLIB

# Checks for libraries.
AC_CHECK_LIB([m], [sqrt])
AS_IF([test "x$with_x11" != "xno"], [PKG_CHECK_MODULES(X11, [x11])])

# Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_CHECK_HEADERS([fcntl.h netinet/in.h sys/socket.h sys/time.h unistd.h])

AC_CHECK_DECL([PF_INET6],
	      [AC_DEFINE([HAVE_IPV6], [1],
			 [The system has IPv6 support.])],
	      [], [#include <sys/socket.h>])
AC_CHECK_DECL([IPV6_V6ONLY],
	      [AC_DEFINE([HAVE_IPV6_V6ONLY], [1],
			 [The system has IPv6 v6 only support.])],
	      [], [#include <netinet/in.h>])

PKG_CHECK_MODULES(FONTSPROTO, [fontsproto])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_C_BIGENDIAN
AC_TYPE_UID_T
AC_TYPE_MODE_T
AC_TYPE_PID_T
AC_TYPE_SIZE_T

# Checks for library functions.
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_MMAP
AC_FUNC_STAT
AC_TYPE_SIGNAL
AC_CHECK_FUNCS([gettimeofday pathconf munmap mkdir rmdir select socket])

# Checks for i18n
AM_GNU_GETTEXT([external])
AM_GNU_GETTEXT_VERSION([0.17])

AC_CONFIG_FILES([
	Makefile
	doc/Makefile
	po/Makefile.in
	libfstt/Makefile
	src/Makefile
])
AC_OUTPUT

